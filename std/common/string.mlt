trait read (∀ t . string -> t optional)

trait show (∀ t . t -> string)


vocab mlatu {
  intrinsic eq-string (string, string -> bool)
  intrinsic gt-string (string, string -> bool)
  intrinsic read-float64 (string -> double optional)
  intrinsic read-int64 (string -> int optional)
  intrinsic show-float64 (double -> string)
  intrinsic show-int64 (int -> string)
  intrinsic string-concat (string, string -> string)
  intrinsic string-from-list (char list -> string)
  intrinsic string-to-list (string -> char list)
}

type string {}

instance + (string, string -> string) { mlatu::string-concat }

instance < (string, string -> bool) { swap (>) }

instance <= (string, string -> bool) { (>) not }

instance <> (string, string -> bool) { (=) not }

instance = (string, string -> bool) { mlatu::eq-string }

instance > (string, string -> bool) { mlatu::gt-string }

instance >= (string, string -> bool) { (<) not }

instance read (string -> bool optional) {
  dup
  if ("true" =) { drop true some }
  else { if ("false" =) { false some } else { none } }
}

instance read (string -> double optional) { mlatu::read-float64 }

instance read (string -> int optional) { mlatu::read-int64 }

instance read (string -> char optional) {
  chars dup
  if (length 1 =) { head }
  else { drop none }
}

instance read (string -> string optional) { some }

instance show (string -> string) {}

instance show (bool -> string) { if { "true" } else { "false" } }

instance show (∀ a b . b a either -> string) {
  match
  case left { show }
  case right { show }
}

instance show (∀ t . t optional -> string) {
  match
  case some -> x { "some " x show + }
  case none { "none" }
}

instance show (double -> string) { mlatu::show-float64 }

instance show (int -> string) { mlatu::show-int64 }

instance show (char -> string) { once from-chars }

define chars (string -> char list) { mlatu::string-to-list }

define from-chars (char list -> string) { mlatu::string-from-list }
