type optional[t] {   case none case some (t) }

define from-optional (∀ t . t optional, t -> t) {
  -> default;
  match
  case some {}
  else { default }
}

define from-some (∀ t . t optional, string -> t) {
  -> message;
  match
  case some {}
  else { message fail }
}

define is-none (∀ t . t optional -> bool) {
  match
  case none { true }
  else { false }
}

define is-some (∀ t . t optional -> bool) {
  match
  case some { drop true }
  else { false }
}

define lift-optional-2 (∀ a b c . a optional, b optional, (a, b -> c) -> c optional) {
  -> mx, my, f;
  match (mx)
  case some -> x { match (my) case some -> y { x y f call some } else { none } }
  else { none }
}

define map-optional (∀ a b . a optional, (a -> b) -> b optional) {
  -> opt, f;
  match (opt)
  case some -> x { x f call some }
  else { none }
}

define optional-fun (∀ r... s... a . r..., a optional, (r..., a -> s...), (r... -> s...) -> s...) {
  -> f, g;
  match
  case some { f call }
  case none { g call }
}
